*//////////////////////////////////////////////////////////////////////
* (C) National Semiconductor, Corporation.
* Models developed and under copyright by:
* National Semiconductor, Corporation.  
*/////////////////////////////////////////////////////////////////////
* Legal Notice:  
* The model may be copied, and distributed without any modifications;
* however, reselling or licensing the material is illegal.
* We reserve the right to make changes to the model without prior notice. 
* Pspice Models are provided "AS IS, WITH NO WARRANTY OF ANY KIND" 
*////////////////////////////////////////////////////////////////////
*		    LMH6645 SPICE MODEL PERFORMANCE
*
*	MODEL FEATURES INCLUDE OUTPUT SWING, OUTPUT CURRENT THRU
*	THE SUPPLY RAILS, GAIN AND PHASE, GAIN CHANGE WITH LOAD,
*	SLEW RATE, COMMON MODE REJECTION WITH FREQUENCY EFFECTS,
*	POWER SUPPLY REJECTION ON BOTH RAILS WITH FREQUENCY
*	EFFECTS, RAIL TO RAIL INPUT AND OUTPUT, VOS AND IB CHANGE
*	WITH COMMON MODE VOLTAGE (INPUT STAGE TRANSITION), INPUT
*	VOLTAGE NOISE WITH 1/F,INPUT CURRENT NOISE WITH 1/F,
*	CLOAD VERSUS CLOSED LOOP GAIN TOLERANCE, AND QUIESCENT
*	SUPPLY CURRENT. MODEL PERFORMANCE CHANGES ONLY SLIGHTLY
*	WITH SUPPLY AS PER DATA SHEET.
*
*	MODEL TEMP RANGE IS -40 TO +125 DEG C.
*
*	NOTE THAT MODEL IS FUNCTIONAL OVER THIS	RANGE BUT NOT ALL
*	PARAMETERS TRACK THOSE OF THE REAL PART.
*
*
*	SPEC			MACRO		DATA
*				MODEL		SHEET
*
*	SUPPLY VOLT RANGE	2.7 TO 10	2.7 TO 10	VOLTS
*
*	AVOL CHANGES W LOAD	YES		YES
*
*	INPUT COMMON MODE	(V+)+0.4	(V+)+0.4	VOLTS
*	VOLTAGE RANGE		TO (V-)-0.4	TO (V-)-0.4
*
*	PHASE REVERSAL IF	NO		NO
*	DRIVEN PAST INPUT
*	COMMON MODE VOLT
*	RANGE
*
*	INPUT CLAMP DIODES	YES		YES
*
*	INPUT DIFFERENTIAL	YES		YES
*	CLAMP DIODES
*
*	OUTPUT CLAMP DIODES	YES		YES
*
*	DUAL INPUT STAGE	YES		YES
*
*	COLLECTOR OUTPUT	YES		YES
*
*	CMRR FREQ EFFECTS	YES		YES
*
*	PSRR FREQ EFFECTS	YES		YES
*
*	
*	INPUT VOLTAGE NOISE	17		17		NV/RHZ
*
*	VOLT NOISE 1/F 1KHZ	23		25		NV/RHZ
*
*	VOLT NOISE 1/F 10 HZ	125		125		NV/RHZ
*
*	INV INPUT I NOISE	0.74		0.75		PA/RHZ
*
*	INV IN INOISE 1/F 1KHZ  1.17		1.2		PA/RHZ
*
*	INV IN INOISE 1/F 10 HZ  9		 9		PA/RHZ
*
*	INPUT BIAS CURRENT	0.6		0.6		UA
*
*	VOS			0.1/0.35	 0.1/0.35	MV
*
*	SLEW RATE 		 22		 22		V/US
*
*	SWING AT 1MA		70		40		MV
*
*	SWING AT 10MA		234		180		MV
*
*	SWING AT 20MA		391		370		MV
*
*	ICC @ 2.7 V		0.65		0.65		MA
*
*	ICC @  10 V		0.725		0.725		MA
*
*	INPUT CAPACITANCE	2		2		PF
*
*	+PSRR / KNEE		105/1 		105/1		DB/KHZ
*
*	-PSRR / KNEE		95/0.3		95/0.3		DB/KHZ
*
*	CMRR / KNEE		77/20		72/20		DB/KHZ
*
*	AVOL @ RL=1K		90		85		DB
*
*	AVOL ZERO CROSS		30		30		MHZ
*
*	AC ZO AT 100KHZ		0.19		0.1		OHM
*
*	AC ZO AT 1 MHZ		1.8		1.2		OHM
*
*	AC ZO AT 10 MHZ		19		20		OHM
*
*	AC ZO AT 60 MHZ		94		200		OHM
*
*	AC ZO AT 200 MHZ	73		90		OHM
*
*	OUPUT CURRENT LIMIT +	68		67		MA
*
*	OUPUT CURRENT LIMIT -	65		61		MA
*
*	BANDWIDTH @ AVCL= +1	56		55		MHZ
*
*	BANDWIDTH @ AVCL= +2	30		31		MHZ
*
*	BANDWIDTH @ AVCL= +10	3.6		3.9		MHZ
*
*	OVERSHOOT @ CLOAD	21		30		%
*	= 10 PF @ AVCL=+1
*
*	OVERSHOOT @ CLOAD	38		30		%
*	= 68 PF @ AVCL=+2
*
*	OVERSHOOT @ CLOAD	41		30		%
*	=390 PF @ AVCL=+3
*
*	OVERSHOOT @ CLOAD	34		30		%
*	=1000 PF @ AVCL=+5
*
*BEGIN MODEL LMH6645
*PINOUT ORDER  -IN +IN V+ V- OUT
*PINOUT ORDER    4  3   5  2  1
.SUBCKT LMH6645 4 3 5 2 1
Q1 6 7 8 QIN
Q2 9 10 11 QIN
Q3 12 13 14 QN
Q4 13 13 14 QN
Q5 15 10 16 QIP
Q6 17 18 19 QIP
Q7 20 21 22 QP
Q12 23 24 25 QP
Q13 26 26 22 QP
Q14 27 27 26 QP
Q15 25 28 22 QP
Q16 13 29 20 QP
Q17 1 25 30 QOP
Q18 31 31 32 QN
Q19 32 32 33 QN
Q20 23 34 14 QN
Q21 1 23 35 QON
Q22 25 31 23 QN
R1 12 8 125
R2 12 11 125
R3 19 20 125
R4 16 20 125
R5 36 37 200
R7 6 22 300
R8 14 15 300
R9 14 17 300
R10 34 35 100
R11 28 30 100
R12 30 5 11.2
R13 2 35 12
V3 22 29 1.4
I1 21 22 4E-6
I4 24 14 0.85E-3
I5 22 31 0.85E-3
I6 22 25 2E-3
I7 23 14 2E-3
G1 23 14 38 39 -0.4E-3
R15 41 10 200
R16 38 42 50
C2 42 1 10E-12
R17 24 27 11.2
R18 14 33 12
Q24 43 43 7 QP
Q25 10 10 43 QP
Q26 7 7 44 QN
Q27 44 44 10 QN
D1 7 22 D1
D2 10 22 D1
D3 14 18 D1
D4 14 10 D1
D5 1 5 D1
D6 2 1 D1
V5 7 18 -0.35E-3
V6 37 18 -0.1E-3
D7 45 0 DIN
D8 46 0 DIN
I8 0 45 0.1E-3
I9 0 46 0.1E-3
E2 14 0 2 0 1
E3 22 0 5 0 1
C4 36 0 2E-12
C5 4 0 2E-12
D9 47 0 DVN
D10 48 0 DVN
I10 0 47 0.1E-3
I11 0 48 0.1E-3
E4 41 4 47 48 7.8
G2 36 41 45 46 2.8E-4
I12 2 5 0.115E-3
R22 2 5 97.3E3
E5 49 0 22 0 1
E6 50 0 14 0 1
E7 51 0 52 0 1
R30 49 53 1E6
R31 50 54 1E6
R32 51 55 1E6
R33 0 53 100
R34 0 54 100
R35 0 55 10E3
E10 56 3 55 0 8E-4
R36 41 52 1E9
R37 52 36 1E9
C6 49 53 1E-12
C7 50 54 3E-12
C8 51 55 3E-12
R6 9 22 300
E11 57 56 54 0 0.15
E12 36 57 53 0 0.05
G3 38 39 9 6 2E-3
G4 38 39 15 17 2E-3
R40 38 39 1E6
E14 39 14 22 14 0.5
D11 38 22 D1
D12 14 38 D1
C9 9 6 3E-12
C10 15 17 3E-12
.MODEL D1 D
.MODEL QN NPN
.MODEL QP PNP
.MODEL QON NPN VAF=40 RC=2 RE=3 IKF=0.1
.MODEL QOP PNP VAF=40 RC=2 RE=3 IKF=0.1
.MODEL QIN NPN KF=1.25E-15 BF=333
.MODEL QIP PNP KF=1.25E-15 BF=333
.MODEL DVN D KF=1.85E-16
.MODEL DIN D KF=8E-17
.ENDS
*END MODEL LMH6645

